raw `

FallarborTown_MoveRelearnersHouse_MapScripts::
	.byte 0

FallarborTown_MoveRelearnersHouse_EventScript_ChooseMon::
	goto FallarborTown_MoveRelearnersHouse_EventScript_MoveTutorSelectMon

FallarborTown_MoveRelearnersHouse_EventScript_NoMoveToTeachMon::
	msgbox FallarborTown_MoveRelearnersHouse_Text_DontHaveMoveToTeachPokemon, MSGBOX_DEFAULT
	goto FallarborTown_MoveRelearnersHouse_EventScript_MoveTutorSelectMon
	end

FallarborTown_MoveRelearnersHouse_EventScript_CantTeachEgg::
	msgbox FallarborTown_MoveRelearnersHouse_Text_CantTeachEgg, MSGBOX_DEFAULT
	goto FallarborTown_MoveRelearnersHouse_EventScript_MoveTutorSelectMon
	end

FallarborTown_MoveRelearnersHouse_Text_TutorWhichMon:
	.string "Which POKéMON needs tutoring?$"

FallarborTown_MoveRelearnersHouse_Text_TeachWhichMove:
	.string "Which move should I teach?$"

FallarborTown_MoveRelearnersHouse_Text_DontHaveMoveToTeachPokemon:
	.string "Sorry…\p"
	.string "It doesn't appear as if I have any move\n"
	.string "I can teach that POKéMON.$"

FallarborTown_MoveRelearnersHouse_Text_CantTeachEgg:
	.string "Hunh? There isn't a single move that\n"
	.string "I can teach an EGG.$"

`
script FallarborTown_MoveRelearnersHouse_EventScript_MoveRelearner {
	lock
	faceplayer
	setflag(FLAG_MOVE_TUTOR_IGNORE_LEVEL)
	msgbox(format("I'm the POKéMON move teacher!"))
	msgbox(format("I can teach a POKéMON any move they learn as they grow stronger - even those they haven't learned yet!"))
	goto(FallarborTown_MoveRelearnersHouse_EventScript_MoveTutorDisplayPrice)
}

script FallarborTown_MoveRelearnersHouse_EventScript_MoveTutor {
	lock
	faceplayer
	setflag(FLAG_MOVE_TUTOR_LEARNSET)
	msgbox(format("I'm the POKéMON move tutor!"))
	msgbox(format("I can teach a POKéMON moves which they can't learn normally - even those they learn from other POKéMON!"))
	goto(FallarborTown_MoveRelearnersHouse_EventScript_MoveTutorDisplayPrice)
}

script FallarborTown_MoveRelearnersHouse_EventScript_MoveTutorDisplayPrice {
	showmoneybox(0,0)
	msgbox(format("This service costs a mere ¥500 per move!"))
	goto(FallarborTown_MoveRelearnersHouse_EventScript_MoveTutorSelectMon)
}
script FallarborTown_MoveRelearnersHouse_EventScript_MoveTutorSelectMon {
	
	# Check player can afford
	checkmoney(500)
	if (var(VAR_RESULT) == FALSE){
		goto(FallarborTown_MoveRelearnersHouse_EventScript_MoveTutorCantAfford)
	}

	msgbox (FallarborTown_MoveRelearnersHouse_Text_TutorWhichMon, MSGBOX_DEFAULT)
	special(ChooseMonForMoveRelearner)
	waitstate
	
	# No party Pokemon chosen
	if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) {
		goto(FallarborTown_MoveRelearnersHouse_EventScript_MoveTutorEnd)
	}

	# Check if pokemon is egg
	special(IsSelectedMonEgg)
	
	# Pokemon is egg
	if (var(VAR_RESULT) == TRUE) {
		goto(FallarborTown_MoveRelearnersHouse_EventScript_CantTeachEgg)
	}

	# No moves to teach Pokemon
	if (var(VAR_0x8005) == 0) {
		goto(FallarborTown_MoveRelearnersHouse_EventScript_NoMoveToTeachMon)
	}

	# Jump to move selection
	goto(FallarborTown_MoveRelearnersHouse_EventScript_MoveTutorSelectMove)
}

script FallarborTown_MoveRelearnersHouse_EventScript_MoveTutorSelectMove {
	# Ask which move to teach
	msgbox(FallarborTown_MoveRelearnersHouse_Text_TeachWhichMove, MSGBOX_DEFAULT)
	special(TeachMoveRelearnerMove)
	waitstate
	if (var(VAR_0x8004) == 0){
		goto(FallarborTown_MoveRelearnersHouse_EventScript_MoveTutorEnd)
	}
	showmoneybox(0,0)
	msgbox(format("I'll accept your payment now, please."))
	removemoney(500)
	playse(SE_SHOP)
	updatemoneybox
	waitse
	goto(FallarborTown_MoveRelearnersHouse_EventScript_MoveTutorAsk)
	end
}

script FallarborTown_MoveRelearnersHouse_EventScript_MoveTutorAsk {
	msgbox(format("Would you like me to teach anything else?"))
	yesnobox(0, 8)
    if (var(VAR_RESULT) == YES){
        goto(FallarborTown_MoveRelearnersHouse_EventScript_MoveTutorSelectMon)
    } else {
		goto(FallarborTown_MoveRelearnersHouse_EventScript_MoveTutorEnd)
	}
}

script FallarborTown_MoveRelearnersHouse_EventScript_MoveTutorCantAfford {
	msgbox(format("Sorry, you need ¥2000 for me to teach your POKéMON a move!"))
	goto(FallarborTown_MoveRelearnersHouse_EventScript_MoveTutorEnd)
}

script FallarborTown_MoveRelearnersHouse_EventScript_MoveTutorEnd {
	msgbox(format("Please come back again soon!"))
	clearflag(FLAG_MOVE_TUTOR_IGNORE_LEVEL)
	clearflag(FLAG_MOVE_TUTOR_LEARNSET)
	hidemoneybox
	release
	end
}