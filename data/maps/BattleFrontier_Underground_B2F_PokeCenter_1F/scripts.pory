raw `
.set LOCALID_NURSE, 1

BattleFrontier_Underground_B2F_PokeCenter_1F_MapScripts::
	map_script MAP_SCRIPT_ON_TRANSITION, BattleFrontier_Underground_B2F_PokeCenter_1F_OnTransition
	map_script MAP_SCRIPT_ON_RESUME, CableClub_OnResume
	.byte 0

BattleFrontier_Underground_B2F_PokeCenter_1F_OnTransition:
	setrespawn HEAL_LOCATION_BATTLE_FRONTIER_UNDERGROUND
	end

BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_Nurse::
	setvar VAR_0x800B, LOCALID_NURSE
	call Common_EventScript_PkmnCenterNurse
	waitmessage
	waitbuttonpress
	release
	end
`

script BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_Gentleman {
    lock
	faceplayer
	msgbox(format("Good to see you. I offer rare items in exchange for battle points."))
	goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GentlemanCheckBattlePoints)
}

script BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GentlemanCheckBattlePoints {
	specialvar(VAR_TEMP_1, GetFrontierBattlePoints)
	if(var(VAR_TEMP_1) >= 50) {
		goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GentlemanCheckSelection)
	}
	msgbox(format("You'll need at least 50 BP before you can buy anything from me."))
	goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GentlemanEnd)
}

script BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GentlemanCheckSelection {
	msgbox(format("What would you like? All of my items cost 50 BP."))
	special(ShowBattlePointsWindow)
	multichoice2(18,0,BattleFrontier_Underground_B2F_PokeCenter_1F_Text_Gentleman)
	switch(var(VAR_RESULT)) {
		case 0:
			giveitem(ITEM_CHERISH_BALL)
		case 1: 
			giveitem(ITEM_GOLD_BOTTLE_CAP)
		default: 
			special(CloseBattlePointsWindow)
			goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GentlemanEnd)
	}
	setvar(VAR_0x8004, 50)
	msgbox(format("I'll take your payment now, please."))
	special(TakeFrontierBattlePoints)
	playse(SE_SHOP)
	special(UpdateBattlePointsWindow)
	waitse
	special(CloseBattlePointsWindow)
    msgbox(format("Thank you for that."))
    msgbox(format("Would you like anything else?"))
    yesnobox(0, 8)
    if (var(VAR_RESULT) == YES) {
        goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GentlemanCheckBattlePoints)
    }
    goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GentlemanEnd)
}

script BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GentlemanEnd {
	msgbox(format("Please come see me again."))
    release
    end
}

script BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GigantamaxExpert {
	lock
	faceplayer
	msgbox(format("Hello, I'm the Gigantamax Expert! I can change the gigantamax factor of your POKéMON."))
	msgbox(format("My services cost a mere ¥500 per POKéMON."))
	goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GigantamaxExpertCheckMoney)
}

script BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GigantamaxExpertCheckMoney {
	showmoneybox(0,0)
	checkmoney(500)
	if(var(VAR_RESULT) == TRUE) {
		goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GigantamaxExpertSelectMon)
	}
	goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GigantamaxExpertCantAfford)
}

script BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GigantamaxExpertSelectMon {
	msgbox(format("Which POKéMON would you like me to change?"))
	special(ChooseMonForMoveRelearner)
	waitstate

	# Gets hidden after mon selection
	showmoneybox(0,0)

	# No party Pokemon chosen
	if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) {
		goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GigantamaxExpertEnd)
	}

	# Check if pokemon is egg
	special(IsSelectedMonEgg)
	
	# Pokemon is egg
	if (var(VAR_RESULT) == TRUE) {
		goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GigantamaxExpertCantTeachEgg)
	}

	// Check if the pokemon is gigantamax
	hasgigantamaxfactor(VAR_0x8004)

	if (var(VAR_RESULT) == TRUE) {
		msgbox(format("Would you like me to remove the Gigantamax factor from this POKéMON?"))
	}
	else {
		msgbox(format("Would you like me to add the Gigantamax factor to this POKéMON?"))
	}
	yesnobox(0, 8)
    if (var(VAR_RESULT) == YES){

        # Toggle gigantamax factor for pokemon
		togglegigantamaxfactor(VAR_0x8004)

		if (var(VAR_RESULT) == TRUE) {
			msgbox(format("Done! Your POKéMON's gigantamax factor has been changed."))
			msgbox(format("I'll accept your payment now, please."))
			removemoney(500)
			playse(SE_SHOP)
			updatemoneybox
			waitse
		} 
		else # Failed to change gigantamax factor
		{
			msgbox(format("Sorry, I was unable to change the gigantamax factor of this POKéMON!"))
		}
    } else {
		hidemoneybox
		goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GigantamaxExpertEnd)
	}
	hidemoneybox
	goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GigantamaxExpertAsk)
}

script BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GigantamaxExpertAsk {
	msgbox(format("Would you like me to teach anything else?"))
	yesnobox(0, 8)
    if (var(VAR_RESULT) == YES){
        goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GigantamaxExpertCheckMoney)
    } else {
		goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GigantamaxExpertEnd)
	}
}

script BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GigantamaxExpertCantAfford {
	msgbox(format("Sorry, you need ¥500 for me to change your POKéMON's gigantamax state!"))
	goto(FallarborTown_MoveRelearnersHouse_EventScript_MoveTutorEnd)
}

script BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GigantamaxExpertCantTeachEgg {
	msgbox(format("I can't change the gigantamax status of an egg!"))
	goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GigantamaxExpertEnd)
}

script BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_GigantamaxExpertEnd {
	msgbox(format("Please come back again soon!"))
	release
	end
}

script BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_BattleFrontierExpert {
	msgbox(format("Hello! I'm the Battle Frontier Expert!"))
	msgbox(format("I can help you customise your Battle Frontier experience."))
	msgbox(format("What would you like to do?"))
	goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_BattleFrontierExpertOptions)
}

script BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_BattleFrontierExpertOptions {
	multichoice2(18,0,BattleFrontier_Underground_B2F_PokeCenter_1F_Text_BattleFrontierExpert)
	switch(var(VAR_RESULT)) {
		case 0: # Change Opponents
			goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_BattleFrontierExpertChangeOpponents)
		case 1: # Change Behavior
			goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_BattleFrontierExpertChangeBehavior)
		case 2: # Change Music
			goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_BattleFrontierExpertChangeMusic)
		default: 
			goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_BattleFrontierExpertEnd)
	}
}

script BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_BattleFrontierExpertChangeOpponents {

	if (flag(FLAG_BATTLE_FRONTIER_GENERATOR) == TRUE) {
		msgbox(format("You are currently playing against modern Battle Frontier opponents."))
		msgbox(format("Keep in mind, you may experience bugs or crashes using this mode during development."))
		msgbox(format("Would you like to switch to legacy Battle Frontier opponents?"))
	}
	else # Frontier Generator not enabled
	{
		msgbox(format("You are currently playing against legacy Battle Frontier opponents."))
		msgbox(format("Would you like to switch to modern Battle Frontier opponents?"))
		msgbox(format("Keep in mind, you may experience bugs or crashes using this mode during development."))
	}

	yesnobox(0, 8)

	if (var(VAR_RESULT) == YES){
		if (flag(FLAG_BATTLE_FRONTIER_GENERATOR) == TRUE) {
			clearflag(FLAG_BATTLE_FRONTIER_GENERATOR) # Clear the flag
			msgbox(format("Done! You'll now play against legacy Battle Frontier opponents."))
		}
		else # Frontier Generator not enabled
		{
			setflag(FLAG_BATTLE_FRONTIER_GENERATOR) # Set the flag
			msgbox(format("Done! You'll now play against modern Battle Frontier opponents."))
		}
	}

	msgbox(format("Feel free to ask me again if you change your mind!"))
	goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_BattleFrontierExpertAsk)
}

script BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_BattleFrontierExpertChangeBehavior {

	msgbox(format("I can change how modern Battle Frontier teams are built."))
	msgbox(format("Which option would you like to use?"))

	multichoice2(18,0,BattleFrontier_Underground_B2F_PokeCenter_1F_Text_BattleFrontierExpertBehavior)
	copyvar(VAR_FRONTIER_METHOD, VAR_RESULT)

	msgbox(format("Modern teams have been updated successfully!"))

	goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_BattleFrontierExpertAsk)
}

script BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_BattleFrontierExpertChangeMusic {
	msgbox(format("What would like to listen to?"))
	msgbox("L: Previous Page                    R: Next Page\nB: Go Back")
	goto(BattleFrontier_Generic_EventScript_MusicAttendantPage1)
}

script BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_BattleFrontierExpertAsk {
	msgbox(format("Do you need me for anything else?"))
	yesnobox(0, 8)
    if (var(VAR_RESULT) == YES){
        goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_BattleFrontierExpertOptions)
    } else {
		goto(BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_BattleFrontierExpertEnd)
	}
}

script BattleFrontier_Underground_B2F_PokeCenter_1F_EventScript_BattleFrontierExpertEnd {
	msgbox(format("I hope you have some good battles!"))
	release
	end
}

text BattleFrontier_Underground_B2F_PokeCenter_1F_Text_Gentleman {
	"CHERISH BALL$"
	"GOLD BOTTLE CAP$"
	"NOTHING$"
	""
}

text BattleFrontier_Underground_B2F_PokeCenter_1F_Text_BattleFrontierExpert {
	"CHANGE OPPONENTS$"
	"CHANGE BEHAVIOR$"
	"CHANGE MUSIC$"
	"NOTHING$"
	""
}

text BattleFrontier_Underground_B2F_PokeCenter_1F_Text_BattleFrontierExpertBehavior {
	"DEFAULT$"
	"RANDOM$"
	"FILTERED$"
	"FILTERED ATTACKS$"
	"RANKED$"
	"RANKED ATTACKS$"
	""
}
