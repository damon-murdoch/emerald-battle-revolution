script Common_EventScript_Legendary_Shop {
    lock
    faceplayer
    msgbox(format("Welcome to the one-and-only legendary POKéMON shop!"))
    goto(Common_EventScript_Legendary_Shop_CheckMoney)
}

script Common_EventScript_Legendary_Shop_CheckMoney {
    showmoneybox(18,0)
    checkmoney(12000)
    if (var(VAR_RESULT) == TRUE){        
        msgbox(format("All Pokemon cost a mere ¥12000."))
        goto(Common_EventScript_Legendary_Shop_CheckGoldBottleCap)
    }
    else # Player does not have enough money
    {
        msgbox(format("Oh, sorry! You need at least ¥12000 to buy anything here!"))
        goto(Auto_EventScript_Legendary_Shop_End)
    }
}

script Common_EventScript_Legendary_Shop_CheckGoldBottleCap {
    checkitem(ITEM_GOLD_BOTTLE_CAP)
    if (var(VAR_RESULT) == TRUE) {
        msgbox(format("Oh, you have a gold bottle cap! Would you redeem it for a shiny Pokemon?"))
        yesnobox(0, 8)
        if (var(VAR_RESULT) == TRUE) {
            clearflag(P_FLAG_FORCE_NO_SHINY)
            setflag(P_FLAG_FORCE_SHINY)
        }
        else {
            setflag(P_FLAG_FORCE_NO_SHINY)
            clearflag(P_FLAG_FORCE_SHINY)
        }
    }
    else {
        setflag(P_FLAG_FORCE_NO_SHINY)
        clearflag(P_FLAG_FORCE_SHINY)
    }

    msgbox(format("What are you looking for today?"))
    goto(Auto_EventScript_Legendary_Shop_Controls)
}

script Common_EventScript_Legendary_Shop_CheckPurchase {
    msgbox(format("Let's see if we can find one of those for you ..."))
    switch(var(VAR_RESULT)){
        case MON_GIVEN_TO_PARTY:
            playse(SE_SUCCESS)
            msgbox(format("Here you go!"))
            msgbox(format("The POKéMON has been added to your party."))
            waitse
        case MON_GIVEN_TO_PC:
            playse(SE_SUCCESS)
            msgbox(format("All done!"))
            msgbox(format("The POKéMON has been sent to your PC boxes."))
            waitse
        case MON_CANT_GIVE:
            playse(SE_FAILURE)
            msgbox(format("It looks like we can't give you any more POKéMON right now - Sorry!"))
            waitse
            goto(Auto_EventScript_Legendary_Shop_End)
    }
    msgbox(format("I'll take your payment now, please."))
    removemoney(12000)
    updatemoneybox
    playse(SE_SHOP)
    waitse
    if (flag(P_FLAG_FORCE_SHINY) == TRUE){
        msgbox(format("You also gave a Gold Bottle Cap in exchange."))
        removeitem(ITEM_GOLD_BOTTLE_CAP, 1)
    }
    msgbox(format("Thanks for your business!"))
    msgbox(format("Can we help you with anything else?"))
    yesnobox(0, 8)
    if (var(VAR_RESULT) == YES){
        goto(Common_EventScript_Legendary_Shop_CheckMoney)
    }
    goto(Auto_EventScript_Legendary_Shop_End)
}

script Auto_EventScript_Legendary_Shop_End {
    msgbox(format("Come back again soon!"))
    clearflag(P_FLAG_FORCE_NO_SHINY)
    clearflag(P_FLAG_FORCE_SHINY)
    hidemoneybox
    releaseall
    end
}

script BattleFrontier_Underground_B1F_EventScript_PokemonShopItemClerk {
    lock
	faceplayer
	message(gText_HowMayIServeYou)
	waitmessage
	pokemart(BattleFrontier_Underground_B1F_Mart_PokemonShopItemClerk)
	msgbox(gText_PleaseComeAgain, MSGBOX_DEFAULT)
	release
	end
}
